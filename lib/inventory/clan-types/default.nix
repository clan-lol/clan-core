{
  self,
  self',
  lib,
  pkgs,
  flakeOptions,
  ...
}:
let
  opts = flakeOptions.flake.type.getSubOptions [ "flake" ];
  clanOpts = opts.clan.type.getSubOptions [ "clan" ];
  include = [
    "directory"
    "inventory"
    "machines"
    "meta"
    "modules"
    "outputs"
    "secrets"
    "templates"
  ];
  jsonschema = self.clanLib.jsonschema.fromOptions {
    typePrefix = "Clan";
    readOnly = {
      input = false;
    };
    typeRenames = {
      ClanInventory = "Inventory";
      InventoryTagsAll = "InventoryTagMachines";
      InventoryTagsDarwin = "InventoryTagMachines";
      InventoryTagsNixos = "InventoryTagMachines";
      InventoryTagsFreeform = "InventoryTagMachines";
      InventoryMachinesItem = "Machine";
      InventoryInstances = "Instances";
      ClanOutputs = "Outputs";
      ClanSecrets = "Secrets";
      InstancesItem = "Instance";
      InstanceRolesItem = "InstanceRole";
      InstanceRoleMachinesItem = "InstanceRoleMachine";
      ClanTemplates = "Templates";
      TemplatesClanItem = "TemplatesClan";
      TemplatesMachineItem = "TemplatesMachine";
      TemplatesDiskoItem = "TemplatesDisko";
    };
  } (lib.filterAttrs (n: _v: lib.elem n include) clanOpts);

  clan-types =
    pkgs.runCommand "clan-types"
      {
        nativeBuildInputs = [
          pkgs.jq
          self'.packages.datamodel-code-generator
        ];
      }
      ''
        mkdir -p $out

        jq . \
          ${builtins.toFile "typing.json" (builtins.toJSON jsonschema)} \
          >$out/typing.json

        header=$(cat <<'EOF'
        # DO NOT EDIT THIS FILE MANUALLY. IT IS GENERATED.
        # This file was generated by running `pkgs/clan-cli/clan_lib/nix_models/update.sh`
        #
        # fmt: off
        EOF
        )
        datamodel-codegen \
          --input $out/typing.json \
          --input-file-type jsonschema \
          --output-model-type typing.TypedDict \
          --target-python-version 3.13 \
          --skip-root-model \
          --use-union-operator \
          --use-title-as-name \
          --use-standard-collections \
          --use-field-description \
          --use-schema-description \
          --use-double-quotes \
          --use-frozen-field \
          --custom-file-header "$header" \
          --disable-future-imports \
          --output $out/typing.py
      '';
in
clan-types
